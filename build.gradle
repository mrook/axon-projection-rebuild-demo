buildscript {
	ext {
		springBootVersion = '2.0.6.RELEASE'
	}

	repositories {
		jcenter()
		mavenCentral()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
		classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.5"
	}
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'com.github.ben-manes.versions'

checkstyle {
	toolVersion "7.6.1"
	configFile rootProject.file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
	source ='src/main/java'
}
checkstyleTest {
	source ='src/test/java'
}

spotbugs {
	ignoreFailures = false
	toolVersion = "3.1.6"
	sourceSets = [sourceSets.main]
	excludeFilter = file("config/spotbugs/excludeFilter.xml")
	reportsDir = file("$project.buildDir/reports/spotbugs")
	effort = "max"
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://artifacts.elastic.co/maven'
	}
}

configurations {
	testRuntime.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
}

dependencies {
	compile 'org.projectlombok:lombok:1.18.2'
	annotationProcessor 'org.projectlombok:lombok:1.18.2'

	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'

	compile 'org.axonframework:axon-spring-boot-starter:4.0'
	compile 'dom4j:dom4j:1.6.1'

	compile 'com.google.code.gson:gson:2.8.5'

	compile 'org.springframework.boot:spring-boot-starter-jdbc'
	compile 'org.flywaydb:flyway-core:5.2.0'
	runtime 'org.postgresql:postgresql:42.2.5'

	compile 'org.elasticsearch:elasticsearch:6.4.2'
	compile 'org.elasticsearch.client:transport:6.4.2'
	compile 'org.elasticsearch.plugin:transport-netty4-client:6.4.2'

	runtime 'org.apache.logging.log4j:log4j-to-slf4j:2.11.1'

	compile 'org.apache.commons:commons-lang3:3.8.1'

	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.axonframework:axon-test:4.0'
	testCompile "org.projectlombok:lombok:1.18.2"

	testRuntime 'org.apache.logging.log4j:log4j-api:2.11.1'
	testRuntime 'org.apache.logging.log4j:log4j-core:2.11.1'
}
